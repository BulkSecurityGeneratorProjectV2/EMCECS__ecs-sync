plugins {
    id 'java-module' // buildSrc/src/main/groovy/java-module.gradle
    id 'application'
    id 'com.github.johnrengelman.shadow'
}

defaultTasks 'shadowJar'

mainClassName = "com.emc.ecs.sync.ctl.EcsSyncCtl"

// TODO: All dependencies should be specified in $rootProjectDir/dependencies.toml
dependencies {
    implementation "org.slf4j:slf4j-api:1.7.32"
    implementation "commons-cli:commons-cli:1.4"
    implementation 'org.apache.logging.log4j:log4j-core:2.17.1'
    implementation "com.sun.jersey:jersey-client:1.19.4"
    // jaxb was removed from Java 11 - jaxb dependencies are provided with Java 8
    implementation "javax.xml.bind:jaxb-api:2.3.1"
    implementation project(':ecs-sync-model')
    file("${rootProject.projectDir}/storage-plugins").eachDir {
        if (it.name.endsWith("-model"))
            implementation project(":storage-plugins:${it.name}")
    }
    file("${rootProject.projectDir}/filter-plugins").eachDir {
        if (it.name.endsWith("-model"))
            implementation project(":filter-plugins:${it.name}")
    }
    implementation project(':ecs-sync-jaxrs')

    runtimeOnly "org.apache.logging.log4j:log4j-slf4j-impl:2.17.1"

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'

    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
    testRuntimeOnly 'org.apache.logging.log4j:log4j-slf4j-impl:2.17.1'
}

test {
    useJUnitPlatform()
}

shadowJar {
    classifier ''
}