/*
 * Copyright 2014 EMC Corporation. All Rights Reserved.
 *
 * Licensed under the Apache License, Version 2.0 (the "License").
 * You may not use this file except in compliance with the License.
 * A copy of the License is located at
 *
 * http://www.apache.org/licenses/LICENSE-2.0.txt
 *
 * or in the "license" file accompanying this file. This file is distributed
 * on an "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either
 * express or implied. See the License for the specific language governing
 * permissions and limitations under the License.
 */
group = 'com.emc.vipr'
version = '1.0'

ext.mainClass = 'com.emc.vipr.sync.ViPRSync'

defaultTasks 'distZip'

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.0.3'
    }
}

apply plugin: 'java'
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'distribution'
apply plugin: 'idea'
apply plugin: 'eclipse'

repositories {
    mavenCentral()
    mavenLocal()
}

dependencies {
    compile files('lib/fplibrary-3.3.718.jar'),
            "commons-logging:commons-logging:1.1.1",
            "log4j:log4j:1.2.16",
            "commons-cli:commons-cli:1.2",
            "com.google.code.gson:gson:2.0",
            "com.emc.vipr:atmos-client:2.2.2",
            "net.sf.jgrapht:jgrapht:0.8.3",
            "mysql:mysql-connector-java:5.1.18",
            "org.springframework:spring-core:3.1.0.RELEASE",
            "org.springframework:spring-jdbc:3.1.0.RELEASE",
            "org.springframework:spring-asm:3.1.0.RELEASE",
            "org.springframework:spring-beans:3.1.0.RELEASE",
            "org.springframework:spring-context:3.1.0.RELEASE",
            "org.springframework:spring-context-support:3.1.0.RELEASE",
            "org.springframework:spring-expression:3.1.0.RELEASE",
            "org.springframework:spring-tx:3.1.0.RELEASE",
            "c3p0:c3p0:0.9.1.2",
            "com.amazonaws:aws-java-sdk:1.7.5",
            "net.java.truevfs:truevfs-profile-default:0.10.6",
            "org.slf4j:slf4j-log4j12:1.7.5",
            "com.filepool:fplibrary:3.3.718"
    testCompile "junit:junit:4.11"
}

javadoc {
    options.quiet()
}

shadowJar {
    classifier ''
    mergeServiceFiles()
    appendManifest {
        attributes 'Main-Class': mainClass,
                'Implementation-Version': project.version,
                'Class-Path': configurations.runtime.collect { it.getName() }.join(' ')
    }
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from "${docsDir}/javadoc"
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

distributions {
    main {
        contents {
            from 'readme.txt'
            from 'license.txt'
            from shadowJar
            from 'script'
            into('sample') {
                from 'sample'
            }
            into('doc') {
                from javadocJar
            }
            into('src') {
                from sourcesJar
            }
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.11'
}
